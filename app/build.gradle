plugins {
    id 'com.android.application'
}

android {
    compileSdk 35
    namespace 'in.hridayan.ashell'

    defaultConfig {
        applicationId "in.hridayan.ashell"
        minSdk 26
        targetSdk 35
        versionCode 54
        versionName "v6.1.0"

        ndk.abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

    if (System.getenv("CI") == "true") {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE_PATH"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }

            debug {
                storeFile file(System.getenv("DEBUG_KEYSTORE_PATH"))
                storePassword System.getenv("DEBUG_KEYSTORE_PASSWORD")
                keyAlias System.getenv("DEBUG_KEY_ALIAS")
                keyPassword System.getenv("DEBUG_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        release {
            signingConfig signingConfigs.findByName("release")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "aShell You ${variant.versionName}-${variant.buildType.name}.apk"
        }
    }

    buildFeatures {
        buildConfig = true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    androidResources { generateLocaleConfig true }
    packagingOptions.jniLibs.useLegacyPackaging = true
}

dependencies {
    implementation libs.gson
    implementation libs.material
    implementation libs.lifecycle.viewmodel
    implementation libs.work.runtime
    implementation libs.preference
    implementation libs.shizuku.api
    implementation libs.shizuku.provider
    implementation libs.libsu.core
    implementation libs.glide
    annotationProcessor libs.glide.compiler
    implementation libs.lottie
    implementation libs.javax.annotation
    implementation libs.circleimageview

    implementation project(':adblib')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation libs.junit
}
