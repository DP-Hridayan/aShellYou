plugins {
    id 'com.android.application'
}

android {
    compileSdk 34
    namespace 'in.hridayan.ashell'

    androidResources {
        generateLocaleConfig true
    }

    defaultConfig {
        applicationId "in.hridayan.ashell"
        minSdk 26
        targetSdk 34

        versionCode 52
        versionName "v6.0.1"
        
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
    }
    
    lint {
        baseline = file("lint-baseline.xml")
    }

    // Load signing properties for local builds
    Properties properties = new Properties()
    File propertiesFile = file('/storage/emulated/0/AndroidIDEProjects/signing-properties/signing-foss.properties')
    if (propertiesFile.exists()) {
        properties.load(new FileInputStream(propertiesFile))
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE_PATH") ?: properties.getProperty('storeFile'))
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: properties.getProperty('storePassword')
            keyAlias System.getenv("KEY_ALIAS") ?: properties.getProperty('keyAlias')
            keyPassword System.getenv("KEY_PASSWORD") ?: properties.getProperty('keyPassword')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            crunchPngs true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def buildTypeName = variant.buildType.name
            outputFileName = "aShell You ${variant.versionName}-${buildTypeName}.apk"
        }
    }

    buildFeatures {
        buildConfig = true
        viewBinding true
    }
    
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
    
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
}

dependencies {
    def lifecycle_version = "2.7.0"
    def material_version = "1.13.0-alpha11"
    def preference_version = "1.2.1"
    def shizuku_version = "13.1.0"
    def libsu_version = "6.0.0"
    def glide_version = "4.16.0"
    def lottie_version = "6.6.3"

    implementation 'com.google.code.gson:gson:2.12.1'
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.work:work-runtime:2.9.0"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "androidx.preference:preference:$preference_version"
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "com.github.topjohnwu.libsu:core:$libsu_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13.2'
    implementation project(':adblib')
    implementation project(':changelogs')
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation "com.airbnb.android:lottie:$lottie_version"
}