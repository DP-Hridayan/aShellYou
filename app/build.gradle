plugins {
    id 'com.android.application'
}

android {
    compileSdk 34
    namespace 'in.hridayan.ashell'

    defaultConfig {
        applicationId "in.hridayan.ashell"
        minSdk 26
        targetSdk 34
        versionCode 53
        versionName "v6.0.2"

        ndk.abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

    signingConfigs {
        release {
            if (System.getenv("CI") == "true") {
                storeFile file(System.getenv("KEYSTORE_PATH"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        release {
            signingConfig signingConfigs.findByName("release")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "aShell You ${variant.versionName}-${variant.buildType.name}.apk"
        }
    }

    buildFeatures {
        buildConfig = true
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    androidResources { generateLocaleConfig true }
    packagingOptions.jniLibs.useLegacyPackaging = true
}

dependencies {
    def versions = [
            lifecycle : "2.7.0",
            material  : "1.13.0-alpha11",
            preference: "1.2.1",
            shizuku   : "13.1.0",
            libsu     : "6.0.0",
            glide     : "4.16.0",
            lottie    : "6.6.4"
    ]

    implementation 'com.google.code.gson:gson:2.12.1'
    implementation "com.google.android.material:material:${versions.material}"
    implementation "androidx.lifecycle:lifecycle-viewmodel:${versions.lifecycle}"
    implementation "androidx.work:work-runtime:2.9.0"
    implementation "androidx.preference:preference:${versions.preference}"
    implementation "dev.rikka.shizuku:api:${versions.shizuku}"
    implementation "com.github.topjohnwu.libsu:core:${versions.libsu}"
    implementation "dev.rikka.shizuku:provider:${versions.shizuku}"
    implementation "com.github.bumptech.glide:glide:${versions.glide}"
    annotationProcessor "com.github.bumptech.glide:compiler:${versions.glide}"
    implementation "com.airbnb.android:lottie:${versions.lottie}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    implementation project(':adblib')
    implementation project(':changelogs')
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.13.2'
}